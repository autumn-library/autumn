---
title: Внедрение настроек приложения
---

# Внедрение настроек приложения

Хорошая поделка состоит не только из желудей и пластилина, но и обвешана маленькими приятными глазу детальками. Причем разные поделки, собранные по одному и тому же чертежу, могут только этими детальками и отличаться.

В примере выше мы передавали Обновлятору1С данные авторизации через переменные среды, читающиеся с помощью `&Дуба` и его `&Завязей`. Но это не единственный способ работы с настройками приложения.

Осень содержит в себе механизм передачи желудю настроек приложения напрямую из конфигурационного файла или переменных окружения. Для этого служит аннотация `&Деталька`. `&Пластилин` помогает связать между собой несколько полноценных `&Желудей`, тогда как `&Деталька` налепляется сразу на нужный `&Желудь`.

> Поддержка из аргументов командной строки в ближайших планах разработки. Еще год-два и точно запилим, честно-честно!

Конфигурационный файл может быть в виде `json`, `yaml` или `ini` файла, называется `autumn-properties.json`/`autumn-properties.yml`/`autumn-properties.ini` соответственно и ищется ОСенью в каталоге запуска приложения или в подкаталоге `src`.

::: code-group

```json [autumn-properties.json]
{
    "Логин": "user",
    "Пароль": "pass",
    "ПрочиеНастройки": {
        "Настроение": "Хорошее"
    }
}
```

:::


А вот так их можно использовать в жёлуде:

::: code-group

```bsl [Классы/Обновлятор1С.os]
&Деталька
Перем Логин;

&Деталька
Перем Пароль;

&Деталька("ПрочиеНастройки.Настроение")
Перем Настроение;

&Деталька(Значение = "ПрочиеНастройки.ОтветНаГлавныйВопросЖизниВселеннойИВсегоОстального", ЗначениеПоУмолчанию = 42)
Перем Ответ;

&Желудь
Процедура ПриСозданииОбъекта()
КонецПроцедуры
```

:::

Вы можете добавлять к желудю любые детальки вне зависимости от того, есть они в конфигурационном файле или нет.

По умолчанию "ОСень" будет пытаться найти значение настройки по имени параметра конструктора: в json есть параметры "Логин" и "Пароль", в конструкторе есть параметры "Логин" и "Пароль", искра, буря, безумие!

JSON обычно содержит вложенные объекты и массивы. Путь к настройкам в таких вложенных структурах можно указать в параметре аннотации `&Деталька` в формате библиотеки [configor](https://github.com/khorevaa/configor), используя точки для объектов и квадратные скобки для массивов.

Не обязательно заставлять пользователя указывать все настройки в конфигурационном файле, если ему могут подойти значения настроек по умолчанию. Для указания значения по умолчанию в `&Детальке` есть параметр `ЗначениеПоУмолчанию`. Поразительно, не правда ли?

## Настройка параметров приложения с помощью СоветДругогоМастера

Для более гибкой настройки системы параметров приложения можно использовать класс `СоветДругогоМастера`. Он позволяет программно управлять источниками конфигурации, включая:

- Прямую установку значений параметров
- Настройку префиксов переменных среды
- Изменение имени конфигурационного файла
- Добавление дополнительных каталогов поиска

::: code-group

```bsl [Настройка конфигурации]
// Создание и настройка советника
Совет = Новый СоветДругогоМастера();

// Установка значений напрямую
ЗначенияДеталек = Новый Соответствие();
ЗначенияДеталек.Вставить("app.name", "МоеПриложение");
ЗначенияДеталек.Вставить("app.version", "1.0.0");

Совет.ЗначенияДеталек(ЗначенияДеталек)
     .ПрефиксПеременныхСреды("MYAPP_")
     .ИмяФайлаСоЗначениямиДеталек("application")
     .ДополнительныйКаталогПоискаФайлаСоЗначениямиДеталек("config");

// Использование в создании поделки
Поделка = Новый Поделка(Совет);
```

:::

Такая конфигурация позволит системе:
1. Искать переменные среды с префиксом `MYAPP_` (например, `MYAPP_APP_NAME`, `MYAPP_DATABASE_HOST`)
2. Загружать файлы `application.json`, `application.yaml`, `application.yml`, `application.ini` из стандартных каталогов и каталога `config`
3. Использовать прямо установленные значения с наивысшим приоритетом
