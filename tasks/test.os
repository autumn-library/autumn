#Использовать 1testrunner
#Использовать fs
#Использовать 1commands

Функция ПрогнатьТесты()
	
	ИмяФорматаЛогФайла = "JUnit";
	Если АргументыКоманднойСтроки.Количество() > 0 Тогда
		ИмяФорматаЛогФайла = АргументыКоманднойСтроки[0];
	КонецЕсли;
	
	Тестер = Новый Тестер;

	ФорматЛогФайла = Тестер.ФорматыЛогФайла()[ИмяФорматаЛогФайла];
	Тестер.УстановитьФорматЛогФайла(ФорматЛогФайла);
	
	ПутьКТестам = "tests";
	ПутьКОтчетуJUnit = ОбъединитьПути("out", ИмяФорматаЛогФайла);
	
	ФС.ОбеспечитьПустойКаталог(ПутьКОтчетуJUnit);
	
	РезультатТестирования = Тестер.ТестироватьКаталог(
		Новый Файл(ПутьКТестам),
		Новый Файл(ПутьКОтчетуJUnit)
	);
	
	ИзолированныеТесты = НайтиФайлы(ОбъединитьПути("tests", "ИзолированныеТесты"), "*.os");
	
	Для Каждого Тест Из ИзолированныеТесты Цикл
		
		Команда = Новый Команда;
		
		Команда.ПоказыватьВыводНемедленно(Истина);
		
		Команда.УстановитьИсполнениеЧерезКомандыСистемы(Ложь);
		
		Команда.УстановитьКоманду("oscript");
		Команда.ДобавитьПараметр(ОбъединитьПути("tasks", "testIsolated.os"));
		Команда.ДобавитьПараметр(Тест.ПолноеИмя);
		Команда.ДобавитьПараметр(ИмяФорматаЛогФайла);
		
		КодВозврата = Команда.Исполнить();
		
		РезультатТестирования = Макс(РезультатТестирования, КодВозврата);
		
	КонецЦикла;
	
	Успешно = РезультатТестирования = 0;
	
	Возврат Успешно;
	
КонецФункции // ПрогнатьТесты()

// основной код

ТекКаталог = ТекущийКаталог();

Попытка
	ТестыПрошли = ПрогнатьТесты();
Исключение
	ТестыПрошли = Ложь;
	Сообщить(СтрШаблон("Тесты через 1testrunner выполнены неудачно
			|%1", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
КонецПопытки;

УстановитьТекущийКаталог(ТекКаталог);

Если НЕ ТестыПрошли Тогда
	ВызватьИсключение "Тестирование завершилось неудачно!";
Иначе
	Сообщить(СтрШаблон("Результат прогона тестов <%1>
			|", ТестыПрошли));
КонецЕсли;