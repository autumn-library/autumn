#Использовать asserts

Перем _Имя;
Перем _ТипЖелудя;
Перем _Характер;
Перем _ПрилепляемыеЧастицы;
Перем _Завязь;
Перем _Прозвища;
Перем _Верховный;
Перем _ВремениИнициализации;
Перем _Свойства;
Перем _Методы;
Перем _РефлекторОбъекта;
Перем КешМетодовПоАннотациям;
Перем КешПрилепляемыеЧастицы;

Функция Имя() Экспорт
	Возврат _Имя;
КонецФункции

Функция ТипЖелудя() Экспорт
	Возврат _ТипЖелудя;
КонецФункции

Функция Характер() Экспорт
	Возврат _Характер;
КонецФункции

Функция ПрилепляемыеЧастицы() Экспорт
	Если КешПрилепляемыеЧастицы = Неопределено Тогда
		КешПрилепляемыеЧастицы = Новый ФиксированныйМассив(_ПрилепляемыеЧастицы);
	КонецЕсли;

	Возврат КешПрилепляемыеЧастицы;
КонецФункции

Функция Завязь() Экспорт
	Возврат _Завязь;
КонецФункции

Функция Прозвища() Экспорт
	Возврат _Прозвища;
КонецФункции

Функция Верховный() Экспорт
	Возврат _Верховный;
КонецФункции

Функция ВремениИнициализации() Экспорт
	Возврат _ВремениИнициализации;
КонецФункции

Функция Свойства() Экспорт
	Возврат _Свойства;
КонецФункции

Функция Методы() Экспорт
	Возврат _Методы;	
КонецФункции

Функция РефлекторОбъекта() Экспорт
	Возврат _РефлекторОбъекта;
КонецФункции

Процедура ПриСозданииОбъекта(ТипЖелудя, Имя, Характер, ПрилепляемыеЧастицы, Завязь, Прозвища, Верховный, ВремениИнициализации)
	Ожидаем.Что(ТипЖелудя).ИмеетТип("Тип");
	Ожидаем.Что(Имя).ИмеетТип("Строка");
	Ожидаем.Что(СтрДлина(Имя)).Больше(0);
	Ожидаем.Что(Характер).ИмеетТип("Строка");
	Ожидаем.Что(ПрилепляемыеЧастицы).ИмеетТип("Массив");
	Ожидаем.Что(Завязь).ИмеетТип("Завязь");
	Ожидаем.Что(Прозвища).ИмеетТип("Массив");
	Ожидаем.Что(Прозвища.Количество()).Больше(0);
	Ожидаем.Что(Верховный).ИмеетТип("Булево");
	Ожидаем.Что(ВремениИнициализации).ИмеетТип("Булево");
	
	_Имя = Имя;
	_ТипЖелудя = ТипЖелудя;
	_Характер = Характер;
	_ПрилепляемыеЧастицы = ПрилепляемыеЧастицы;
	_Завязь = Завязь;
	_Прозвища = Прозвища;
	_Верховный = Верховный;
	_ВремениИнициализации = ВремениИнициализации;

	_РефлекторОбъекта = Новый РефлекторОбъекта(ТипЖелудя);

	_Свойства = _РефлекторОбъекта.ПолучитьТаблицуСвойств();
	_Методы = _РефлекторОбъекта.ПолучитьТаблицуМетодов();

	КешМетодовПоАннотациям = Новый Соответствие();

КонецПроцедуры

Функция НайтиМетодыСАннотациями(АннотацияФильтр) Экспорт

	ИзКеша = КешМетодовПоАннотациям[АннотацияФильтр];
	Если не ИзКеша = Неопределено Тогда
		Возврат ИзКеша;
	КонецЕсли;
	
	Результат = Новый Массив();

	Для Каждого СтрокаТаблицы Из Методы() Цикл

		Если СтрокаТаблицы.Аннотации = Неопределено Тогда
			Продолжить;
		КонецЕсли;

		Для Каждого Аннотация Из СтрокаТаблицы.Аннотации Цикл
			
			Если НРег(Аннотация.Имя) = Нрег(АннотацияФильтр) Тогда
				Результат.Добавить(СтрокаТаблицы);
			КонецЕсли;

		КонецЦикла;

	КонецЦикла;

	КешМетодовПоАннотациям.Вставить(АннотацияФильтр, Результат);

	Возврат Результат;

КонецФункции
