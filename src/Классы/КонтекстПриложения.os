#Использовать "../internal"
#Использовать logos
#Использовать semaphore

Перем ИнициализированныеЖелудиОдиночки;
Перем Заготовки;
Перем ФабрикаЖелудей;

Перем Лог;

Функция ПолучитьОпределенияЖелудей() Экспорт
	Возврат ФабрикаЖелудей.ПолучитьОпределенияЖелудей();
КонецФункции

Функция ПолучитьОпределениеЖелудя(Имя) Экспорт
	Возврат ФабрикаЖелудей.ПолучитьОпределениеЖелудя(Имя);
КонецФункции

Функция ПолучитьЖелудь(Имя) Экспорт

	Если ВРег(Имя) = ВРег("КонтекстПриложения") Тогда
		Возврат ЭтотОбъект;
	КонецЕсли; 

	ОпределениеЖелудя = ФабрикаЖелудей.ПолучитьОпределениеЖелудя(Имя);

	Ожидаем.Что(
		ОпределениеЖелудя, 
		СтрШаблон("Не удалось получить определение Желудя по имени Желудя %1", Имя)
	).Не_().Равно(Неопределено);

	Если ОпределениеЖелудя.Характер() = ХарактерыЖелудей.Компанейский() Тогда
		Желудь = ИнициализироватьКомпанейскийЖелудь(Имя);
	ИначеЕсли ОпределениеЖелудя.Характер() = ХарактерыЖелудей.Одиночка() Тогда
		Желудь = ИнициализироватьЖелудьОдиночку(Имя);
	Иначе
		ВызватьИсключение "Неизвестный характер желудя " + ОпределениеЖелудя.Характер();
	КонецЕсли;

	Возврат Желудь;
КонецФункции

Процедура ЗарегистрироватьЖелудь(Тип, Имя = "") Экспорт
	ФабрикаЖелудей.ЗарегистрироватьЖелудь(Тип, Имя);
КонецПроцедуры

Процедура ЗарегистрироватьДуб(Тип) Экспорт
	ФабрикаЖелудей.ЗарегистрироватьДуб(Тип);
КонецПроцедуры

Процедура ЗарегистрироватьНапильник(Тип) Экспорт
	ФабрикаЖелудей.ЗарегистрироватьНапильник(Тип);
КонецПроцедуры

Процедура ЗарегистрироватьЗаготовку(Тип) Экспорт
	ОпределениеЗаготовки = ФабрикаЖелудей.ЗарегистрироватьЗаготовку(Тип);
	ИмяЗаготовки = ОпределениеЗаготовки.Имя();

	Заготовки.Добавить(ИмяЗаготовки);

	Заготовка = ПолучитьЖелудь(ИмяЗаготовки);
	РефлекторОбъекта = Новый РефлекторОбъекта(Заготовка);
	Если РефлекторОбъекта.ЕстьПроцедура("ПриИнициализацииКонтекста", 1) Тогда
		Заготовка.ПриИнициализацииКонтекста(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

Процедура ПросканироватьКаталог(Каталог) Экспорт
	Файлы = НайтиФайлы(Каталог, "*.os", Истина);
	Для Каждого Файл Из Файлы Цикл
		ТипЖелудя = Неопределено;
		Попытка
			ТипЖелудя = Тип(Файл.ИмяБезРасширения);
		Исключение
			Продолжить;
		КонецПопытки;

		РефлекторОбъекта = Новый РефлекторОбъекта(ТипЖелудя);
		Если РефлекторОбъекта.ПолучитьТаблицуМетодов("Желудь", Ложь).Количество() > 0 Тогда
			ЗарегистрироватьЖелудь(ТипЖелудя);
		ИначеЕсли РефлекторОбъекта.ПолучитьТаблицуМетодов("Дуб", Ложь).Количество() > 0 Тогда
			ЗарегистрироватьДуб(ТипЖелудя);
		ИначеЕсли РефлекторОбъекта.ПолучитьТаблицуМетодов("Напильник", Ложь).Количество() > 0 Тогда
			ЗарегистрироватьНапильник(ТипЖелудя);
		ИначеЕсли РефлекторОбъекта.ПолучитьТаблицуМетодов("Заготовка", Ложь).Количество() > 0 Тогда
			ЗарегистрироватьЗаготовку(ТипЖелудя);
		Иначе // BSLLS:EmptyCodeBlock-off
			// no-op
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ЗапуститьПриложение() Экспорт
	Для Каждого ИмяЗаготовки Из Заготовки Цикл
		Заготовка = ПолучитьЖелудь(ИмяЗаготовки);
		РефлекторОбъекта = Новый РефлекторОбъекта(Заготовка);
		Если РефлекторОбъекта.ЕстьПроцедура("ПриЗапускеПриложения", 0) Тогда
			Заготовка.ПриЗапускеПриложения();
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Функция ИнициализироватьКомпанейскийЖелудь(Имя)

	Желудь = Неопределено;

	Попытка
		Желудь = ФабрикаЖелудей.ПолучитьЖелудь(ЭтотОбъект, Имя);
	Исключение
		Лог.Ошибка("Не удалось инициализировать желудь %1", Имя);
		Лог.Ошибка(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;

	Возврат Желудь;

КонецФункции

Функция ИнициализироватьЖелудьОдиночку(Имя)

	Желудь = ИнициализированныеЖелудиОдиночки.Получить(Имя);

	// double-lock checking
	Если Желудь = Неопределено Тогда
		Попытка
			Семафор = Семафоры.Получить(Имя);
			Семафор.Захватить();
			
			Желудь = ИнициализированныеЖелудиОдиночки.Получить(Имя);
			Если Желудь = Неопределено Тогда
				Желудь = ФабрикаЖелудей.ПолучитьЖелудь(ЭтотОбъект, Имя);
				ИнициализированныеЖелудиОдиночки.Вставить(Имя, Желудь);
			КонецЕсли;
			
			Семафор.Освободить();
		Исключение
			Лог.Ошибка("Не удалось инициализировать желудь %1", Имя);
			Лог.Ошибка(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			Семафор.Освободить();
		КонецПопытки;
	КонецЕсли;

	Возврат Желудь;

КонецФункции

Процедура ПриСозданииОбъекта()
	ИнициализированныеЖелудиОдиночки = Новый Соответствие();
	Заготовки = Новый Массив();
	ФабрикаЖелудей = Новый ФабрикаЖелудей();

	ФабрикаЖелудей.ЗарегистрироватьСистемныйНапильник(Тип("ОбработкаНапильникомПластилинаНаПолях"));
	ФабрикаЖелудей.ЗарегистрироватьСистемныйНапильник(Тип("ОбработкаНапильникомФинальныйШтрих"));

	Лог = Логирование.ПолучитьЛог("oscript.lib.autumn.application.context");
КонецПроцедуры
