#Использовать "../internal"
#Использовать logos
#Использовать semaphore

Перем ИнициализированныеЖелудиОдиночки;
Перем Рогатки;
Перем ФабрикаЖелудей;
Перем СостояниеПриложения;

Перем Лог;

Функция ПолучитьОпределенияЖелудей() Экспорт
	Возврат ФабрикаЖелудей.ПолучитьОпределенияЖелудей();
КонецФункции

Функция ПолучитьОпределениеЖелудя(Имя) Экспорт
	Возврат ФабрикаЖелудей.ПолучитьОпределениеЖелудя(Имя);
КонецФункции

Функция ПолучитьЖелудь(Имя) Экспорт

	Если ВРег(Имя) = ВРег("КонтекстПриложения") Тогда
		Возврат ЭтотОбъект;
	КонецЕсли;

	ПроверитьСостояниеВыполнение();

	ОпределениеЖелудя = ФабрикаЖелудей.ПолучитьОпределениеЖелудя(Имя);

	Ожидаем.Что(
		ОпределениеЖелудя, 
		СтрШаблон("Не удалось получить определение Желудя по имени Желудя %1", Имя)
	).Не_().Равно(Неопределено);

	Если ОпределениеЖелудя.Характер() = ХарактерыЖелудей.Компанейский() Тогда
		Желудь = ИнициализироватьКомпанейскийЖелудь(Имя);
	ИначеЕсли ОпределениеЖелудя.Характер() = ХарактерыЖелудей.Одиночка() Тогда
		Желудь = ИнициализироватьЖелудьОдиночку(Имя);
	Иначе
		ВызватьИсключение "Неизвестный характер желудя " + ОпределениеЖелудя.Характер();
	КонецЕсли;

	Возврат Желудь;

КонецФункции

Функция ЗарегистрироватьЖелудь(Тип, Имя = "") Экспорт
	ПроверитьСостояниеИнициализация();
	ФабрикаЖелудей.ЗарегистрироватьЖелудь(Тип, Имя);

	Возврат ЭтотОбъект;
КонецФункции

Функция ЗарегистрироватьДуб(Тип) Экспорт
	ПроверитьСостояниеИнициализация();
	ФабрикаЖелудей.ЗарегистрироватьДуб(Тип);

	Возврат ЭтотОбъект;
КонецФункции

Функция ЗарегистрироватьНапильник(Тип) Экспорт
	ПроверитьСостояниеИнициализация();
	ФабрикаЖелудей.ЗарегистрироватьНапильник(Тип);

	Возврат ЭтотОбъект;
КонецФункции

Функция ЗарегистрироватьЗаготовку(Тип) Экспорт
	
	ПроверитьСостояниеИнициализация();

	РефлекторОбъекта = Новый РефлекторОбъекта(Тип);
	
	Ожидаем
		.Что(
			РефлекторОбъекта.ЕстьПроцедура("ПриИнициализацииКонтекста", 1),
			"Заготовка должна иметь процедуру ПриИнициализацииКонтекста(КонтекстПриложения)"
		)
		.ЭтоИстина();

	Ожидаем
		.Что(
			РефлекторОбъекта.ЕстьПроцедура("ПриСозданииОбъекта", 0),
			"Заготовка должна иметь процедуру ПриСозданииОбъекта()"
		)
		.ЭтоИстина();

	Заготовка = Новый(Тип);
	Заготовка.ПриИнициализацииКонтекста(ЭтотОбъект);

	Возврат ЭтотОбъект;

КонецФункции

Функция ЗарегистрироватьРогатку(Тип) Экспорт
	
	ПроверитьСостояниеИнициализация();

	ОпределениеЗаготовки = ФабрикаЖелудей.ЗарегистрироватьРогатку(Тип);
	ИмяЗаготовки = ОпределениеЗаготовки.Имя();

	Рогатки.Добавить(ИмяЗаготовки);

	Возврат ЭтотОбъект;

КонецФункции

Функция ПросканироватьКаталог(Каталог) Экспорт

	Файлы = НайтиФайлы(Каталог, "*.os", Истина);
	Для Каждого Файл Из Файлы Цикл
		ТипЖелудя = Неопределено;
		Попытка
			ТипЖелудя = Тип(Файл.ИмяБезРасширения);
		Исключение
			Продолжить;
		КонецПопытки;

		РефлекторОбъекта = Новый РефлекторОбъекта(ТипЖелудя);
		Если РефлекторОбъекта.ПолучитьТаблицуМетодов("Желудь", Ложь).Количество() > 0 Тогда
			ЗарегистрироватьЖелудь(ТипЖелудя);
		ИначеЕсли РефлекторОбъекта.ПолучитьТаблицуМетодов("Дуб", Ложь).Количество() > 0 Тогда
			ЗарегистрироватьДуб(ТипЖелудя);
		ИначеЕсли РефлекторОбъекта.ПолучитьТаблицуМетодов("Напильник", Ложь).Количество() > 0 Тогда
			ЗарегистрироватьНапильник(ТипЖелудя);
		ИначеЕсли РефлекторОбъекта.ПолучитьТаблицуМетодов("Заготовка", Ложь).Количество() > 0 Тогда
			ЗарегистрироватьЗаготовку(ТипЖелудя);
		ИначеЕсли РефлекторОбъекта.ПолучитьТаблицуМетодов("Рогатка", Ложь).Количество() > 0 Тогда
			ЗарегистрироватьРогатку(ТипЖелудя);
		Иначе // BSLLS:EmptyCodeBlock-off
			// no-op
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЭтотОбъект;

КонецФункции

Процедура ЗапуститьПриложение() Экспорт

	ПроверитьСостояниеИнициализация();

	СостояниеПриложения = СостоянияПриложения.Выполнение();

	Для Каждого ИмяРогатки Из Рогатки Цикл
		Рогатка = ПолучитьЖелудь(ИмяРогатки);
		РефлекторОбъекта = Новый РефлекторОбъекта(Рогатка);
		Если РефлекторОбъекта.ЕстьПроцедура("ПриЗапускеПриложения", 0) Тогда
			Рогатка.ПриЗапускеПриложения();
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Функция ИнициализироватьКомпанейскийЖелудь(Имя)

	Желудь = Неопределено;

	Попытка
		Желудь = ФабрикаЖелудей.ПолучитьЖелудь(ЭтотОбъект, Имя);
	Исключение
		Лог.Ошибка("Не удалось инициализировать желудь %1", Имя);
		Лог.Ошибка(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;

	Возврат Желудь;

КонецФункции

Функция ИнициализироватьЖелудьОдиночку(Имя)

	Желудь = ИнициализированныеЖелудиОдиночки.Получить(Имя);

	// double-lock checking
	Если Желудь = Неопределено Тогда
		Попытка
			Семафор = Семафоры.Получить(Имя);
			Семафор.Захватить();
			
			Желудь = ИнициализированныеЖелудиОдиночки.Получить(Имя);
			Если Желудь = Неопределено Тогда
				Желудь = ФабрикаЖелудей.ПолучитьЖелудь(ЭтотОбъект, Имя);
				ИнициализированныеЖелудиОдиночки.Вставить(Имя, Желудь);
			КонецЕсли;
			
			Семафор.Освободить();
		Исключение
			Лог.Ошибка("Не удалось инициализировать желудь %1", Имя);
			Лог.Ошибка(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			Семафор.Освободить();
		КонецПопытки;
	КонецЕсли;

	Возврат Желудь;

КонецФункции

Процедура ПроверитьСостояниеИнициализация()

	Ожидаем.
		Что(СостояниеПриложения, "Приложение не находится в состоянии инициализации. Операция запрещена.")
		.Равно(СостоянияПриложения.Инициализация());
	
КонецПроцедуры

Процедура ПроверитьСостояниеВыполнение()

	Ожидаем.
		Что(СостояниеПриложения, "Приложение не находится в состоянии выполнения. Операция запрещена.")
		.Равно(СостоянияПриложения.Выполнение());
	
КонецПроцедуры


Процедура ПриСозданииОбъекта()
	ИнициализированныеЖелудиОдиночки = Новый Соответствие();
	Рогатки = Новый Массив();
	ФабрикаЖелудей = Новый ФабрикаЖелудей();
	СостояниеПриложения = СостоянияПриложения.Инициализация();

	ФабрикаЖелудей.ЗарегистрироватьСистемныйНапильник(Тип("ОбработкаНапильникомПластилинаНаПолях"));
	ФабрикаЖелудей.ЗарегистрироватьСистемныйНапильник(Тип("ОбработкаНапильникомФинальныйШтрих"));

	Лог = Логирование.ПолучитьЛог("oscript.lib.autumn.application.context");

	Заготовки = Осень.ПолучитьЗаготовкиДляАвтоИнициализации();

	Для Каждого ИмяТипаЗаготовки Из Заготовки Цикл
		ЗарегистрироватьЗаготовку(Тип(ИмяТипаЗаготовки));
	КонецЦикла;

КонецПроцедуры
