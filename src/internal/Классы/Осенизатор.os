#Использовать collectionos
#Использовать annotations

#Область ОписаниеПеременных

Перем ФабрикаЖелудей;
Перем КонтейнерАннотаций;
Перем УправляющийПрилепляемымиКоллекциями;
Перем Поделка;
Перем Приемки;
Перем ПросканированныеТипы;
Перем Рефлектор;

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПросканироватьИзвестныеТипы() Экспорт

	Пока Истина Цикл

		ИзвестныеТипы = Рефлектор
			.ИзвестныеТипы(Новый Структура("Пользовательский", Истина));

		Если ПросканированныеТипы.Количество() = ИзвестныеТипы.Количество() Тогда
			Прервать;
		КонецЕсли;

		ПросканироватьТипы(ИзвестныеТипы.ВыгрузитьКолонку("Значение"));

	КонецЦикла;

	Для Каждого Приемка Из Приемки Цикл
		УдалитьОбработчик ФабрикаЖелудей.ПриДобавленииОпределенияЖелудя, Приемка.ПриДобавленииОпределенияЖелудя;
	КонецЦикла;

	Приемки.Очистить();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПросканироватьТипы(Типы)

	РазворачивательАннотаций = КонтейнерАннотаций.ПолучитьРазворачивательАннотаций();

	// Двойной проход по типам для предварительного добавления аннотаций, которые могут быть нужны
	// для добавления остальных типов желудей.
	Для Каждого ТипЖелудя Из Типы Цикл

		Если ПросканированныеТипы.Содержит(ТипЖелудя) Тогда
			Продолжить;
		КонецЕсли;

		Методы = Рефлектор.ПолучитьТаблицуМетодов(ТипЖелудя);

		Если ЕстьМетодСАннотацией(Методы, "Аннотация") Тогда
			ДобавитьАннотацию(ТипЖелудя);
			ПросканированныеТипы.Добавить(ТипЖелудя);
		КонецЕсли;

	КонецЦикла;

	Для Каждого ТипЖелудя Из Типы Цикл

		Если ПросканированныеТипы.Содержит(ТипЖелудя) Тогда
			Продолжить;
		КонецЕсли;

		Методы = Рефлектор.ПолучитьТаблицуМетодов(ТипЖелудя);

		Если ЕстьМетодСАннотацией(Методы, "Аннотация") Тогда
			Продолжить;
		КонецЕсли;

		РазворачивательАннотаций.РазвернутьАннотацииСвойств(Методы, ТипЖелудя);

		Если ЕстьМетодСАннотацией(Методы, "Желудь") Тогда
			ДобавитьЖелудь(ТипЖелудя);
		ИначеЕсли ЕстьМетодСАннотацией(Методы, "Дуб") Тогда
			ДобавитьДуб(ТипЖелудя);
		ИначеЕсли ЕстьМетодСАннотацией(Методы, "Напильник") Тогда
			ДобавитьНапильник(ТипЖелудя);
		ИначеЕсли ЕстьМетодСАннотацией(Методы, "Рогатка") Тогда
			ДобавитьРогатку(ТипЖелудя);
		ИначеЕсли ЕстьМетодСАннотацией(Методы, "Заготовка") Тогда
			ДобавитьЗаготовку(ТипЖелудя);
		ИначеЕсли ЕстьМетодСАннотацией(Методы, "ПрилепляемаяКоллекция") Тогда
			ДобавитьПрилепляемуюКоллекцию(ТипЖелудя);
		ИначеЕсли ЕстьМетодСАннотацией(Методы, "Приемка") Тогда
			ДобавитьПриемку(ТипЖелудя);
		Иначе // BSLLS:EmptyCodeBlock-off
			// no-op
		КонецЕсли;

		ПросканированныеТипы.Добавить(ТипЖелудя);

	КонецЦикла;

КонецПроцедуры

Процедура ДобавитьЖелудь(Тип, Имя = "")
	ФабрикаЖелудей.ДобавитьЖителяЛеса(Тип, Имя, "Желудь");
КонецПроцедуры

Процедура ДобавитьДуб(Тип)
	ФабрикаЖелудей.ДобавитьДуб(Тип);
КонецПроцедуры

Процедура ДобавитьНапильник(Тип)

	ФабрикаЖелудей.ДобавитьНапильник(Тип);

КонецПроцедуры

Процедура ДобавитьЗаготовку(Тип)

	ОпределениеЗаготовки = ФабрикаЖелудей.ДобавитьЖителяЛеса(Тип, "", "Заготовка");

	Заготовка = Поделка.НайтиЖелудь(ОпределениеЗаготовки.Имя());
	Заготовка.ПриИнициализацииПоделки(Поделка);

КонецПроцедуры

Процедура ДобавитьРогатку(Тип)
	ФабрикаЖелудей.ДобавитьЖителяЛеса(Тип, "", "Рогатка");
КонецПроцедуры

Процедура ДобавитьАннотацию(Тип)
	КонтейнерАннотаций.ДобавитьАннотацию(Тип);
КонецПроцедуры

Процедура ДобавитьПрилепляемуюКоллекцию(Тип)
	УправляющийПрилепляемымиКоллекциями.ДобавитьПрилепляемуюКоллекцию(Тип);
КонецПроцедуры

Процедура ДобавитьПриемку(Тип)

	ОпределениеПриемки = ФабрикаЖелудей.ДобавитьЖителяЛеса(Тип, "", "Приемка");

	Приемка = Поделка.НайтиЖелудь(ОпределениеПриемки.Имя());
	ДобавитьОбработчик ФабрикаЖелудей.ПриДобавленииОпределенияЖелудя, Приемка.ПриДобавленииОпределенияЖелудя;

	Приемки.Добавить(Приемка);

КонецПроцедуры

Функция ЕстьМетодСАннотацией(Методы, ИмяАннотации)
	Возврат РаботаСАннотациями.НайтиМетодыСАннотацией(Методы, ИмяАннотации).Количество() > 0;
КонецФункции

#КонецОбласти

Процедура ПриСозданииОбъекта(пПоделка, пФабрикаЖелудей, пКонтейнерАннотаций, пУправляющийПрилепляемымиКоллекциями)

	ФабрикаЖелудей     = пФабрикаЖелудей;
	КонтейнерАннотаций = пКонтейнерАннотаций;
	Поделка            = пПоделка;

	УправляющийПрилепляемымиКоллекциями = пУправляющийПрилепляемымиКоллекциями;

	Приемки = Новый Массив;

	ПросканированныеТипы = Новый МножествоСоответствие();

	Рефлектор = Новый Рефлектор;

КонецПроцедуры
