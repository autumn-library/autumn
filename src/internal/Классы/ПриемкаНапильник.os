#Использовать reflector

Процедура ПриДобавленииОпределенияЖелудя(Знач ОпределениеЖелудя) Экспорт

	ЭтоНапильник = ОпределениеЖелудя.Прозвища().Найти("Напильник") <> Неопределено;
	Если НЕ ЭтоНапильник Тогда
		Возврат;
	КонецЕсли;

	РефлекторОбъекта = Новый РефлекторОбъекта(ОпределениеЖелудя.ТипЖелудя());
	
	КоличествоПараметров = 2;
	Ожидаем
		.Что(
			РефлекторОбъекта.ЕстьФункция("ОбработатьЖелудь", КоличествоПараметров),
			"Напильник должен иметь функцию ОбработатьЖелудь(Желудь, ОпределениеЖелудя)"
		)
		.ЭтоИстина();

	Порядок = ОпределениеЖелудя.Порядок();

	МинимальныйПорядок = 0;
	Если Порядок < МинимальныйПорядок Тогда
		ВызватьИсключение "Неверное значение параметра ""Порядок"". Порядок не может быть меньше, чем " + МинимальныйПорядок;
	КонецЕсли;

	МаксимальныйПорядок = 999999;
	Если Порядок > МаксимальныйПорядок Тогда
		ВызватьИсключение "Неверное значение параметра ""Порядок"". Порядок не может быть больше, чем " + МаксимальныйПорядок;
	КонецЕсли;

	Системный = ОпределениеЖелудя.Имя() = "ОбработкаНапильникомФинальныйШтрих"
		ИЛИ ОпределениеЖелудя.Имя() = "ОбработкаНапильникомПластилинаНаПолях";

	Если НЕ Системный Тогда
		Если Порядок = МинимальныйПорядок ИЛИ Порядок = МаксимальныйПорядок Тогда
			ВызватьИсключение "Неверное значение параметра ""Порядок"". Использовано зарезервированное значение " + Порядок;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&Приемка
Процедура ПриСозданииОбъекта()
	
КонецПроцедуры