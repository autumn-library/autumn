#Использовать decorator

Функция ОбработатьЖелудь(Желудь, ОпределениеЖелудя) Экспорт
	
	Методы = ОпределениеЖелудя.НайтиМетодыСАннотациями("Асинх");

	Если Методы.Количество() = 0 Тогда
		Возврат Желудь;
	КонецЕсли;

	ПостроительДекоратора = Новый ПостроительДекоратора(Желудь);

	Параметр_ИмяМетода = Новый ПараметрМетода("НапильникАсинх_ИмяМетода");
	Параметр_ПараметрыМетода = Новый ПараметрМетода("НапильникАсинх_ПараметрыМетода");
	
	ТелоСлужебногоМетода = 
	"НапильникАсинх_ЗначениеПоля = Декоратор_Рефлектор.ПолучитьСвойство(
	|	ЭтотОбъект,
	|	Константы_Декоратор.Поле_ИнстансОбъекта
	|);
	|
	|НапильникАсинх_Результат = Декоратор_Рефлектор.ВызватьМетод(
	|	НапильникАсинх_ЗначениеПоля,
	|	НапильникАсинх_ИмяМетода,
	|	НапильникАсинх_ПараметрыМетода
	|);
	|
	|ОбработкаДекоратора.СинхронизироватьПоля(НапильникАсинх_ЗначениеПоля, ЭтотОбъект);
	|
	|Возврат НапильникАсинх_Результат;
	|";

	СлужебыйМетод = Новый Метод("НапильникАсинх_Служебный")
		.Публичный()
		.Параметр(Параметр_ИмяМетода)
		.Параметр(Параметр_ПараметрыМетода)
		.ТелоМетода(ТелоСлужебногоМетода);
	
	ПостроительДекоратора.Метод(СлужебыйМетод);

	ШаблонТелаПерехватчика = 
	"НапильникАсинх_ПараметрыМетода = Новый Массив;
	|НапильникАсинх_ПараметрыМетода.Добавить(""%1"");
	|
	|НапильникАсинх_Метод = Декоратор_Рефлектор.ПолучитьТаблицуМетодов(ЭтотОбъект).Найти(""%1"", ""Имя"");
	|НапильникАсинх_ЗначенияПараметровМетода = Новый Массив;
	|Для Каждого НапильникАсинх_Параметр Из НапильникАсинх_Метод.Параметры Цикл
	|	НапильникАсинх_ЗначенияПараметровМетода.Добавить(Вычислить(НапильникАсинх_Параметр.Имя));
	|КонецЦикла;
	|
	|НапильникАсинх_ПараметрыМетода.Добавить(НапильникАсинх_ЗначенияПараметровМетода);
	|
	|ФоновоеЗадание = ФоновыеЗадания.Выполнить(ЭтотОбъект, ""НапильникАсинх_Служебный"", НапильникАсинх_ПараметрыМетода);
	|
	|Возврат ФоновоеЗадание;
	|";

	Для Каждого Метод Из Методы Цикл
		
		Перехватчик = Новый Перехватчик(Метод.Имя)
			.ТипПерехватчика(ТипыПерехватчиковМетода.Перед)
			.Тело(СтрШаблон(ШаблонТелаПерехватчика, Метод.Имя));

		ПостроительДекоратора.Перехватчик(Перехватчик);

	КонецЦикла;
	
	Возврат ПостроительДекоратора.Построить();

КонецФункции

&Напильник
Процедура ПриСозданииОбъекта()

КонецПроцедуры
