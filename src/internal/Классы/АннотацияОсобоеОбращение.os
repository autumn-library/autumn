#Использовать asserts

Перем _ОтключитьВсеНапильники;
Перем _ПрименятьТолькоНапильники;
Перем _НеПрименятьНапильники;

// Проверяет, может ли указанный напильник применяться над желудем, помеченным аннотацией `&ОсобоеОбращение`.
//
// Параметры:
//   ИмяНапильника - Строка - Имя напильника для проверки применимости.
//
//  Возвращаемое значение:
//   Булево - Истина, если напильник может применяться на текущем желуде, иначе Ложь.
//
Функция НапильникМожетПрименяться(ИмяНапильника) Экспорт

	Если _ОтключитьВсеНапильники Тогда
		Возврат Ложь;
	КонецЕсли;

	Если _ПрименятьТолькоНапильники.Количество() > 0 Тогда
		Возврат _ПрименятьТолькоНапильники.Найти(ИмяНапильника) <> Неопределено;
	Иначе
		Возврат _НеПрименятьНапильники.Найти(ИмяНапильника) = Неопределено;
	КонецЕсли;

КонецФункции

// Маркерная аннотация, показывающая, что желудь требует изменений в процессе своего создания.
//
// Параметры аннотации конфликтуют между собой. Если отключаются все напильники, то нельзя задавать ограничения на
// применение конкретных напильников или отключение конкретных напильников. Если задано ограничение на применение только
// конкретных напильников, то нельзя задавать ограничение на отключение конкретных напильников (и наоборот).
//
// Размещается над конструктором класса или над методом `&Завязи`.
//
// Параметры:
//   ОтключитьВсеНапильники - Булево - Не применять на желуде никакие напильники.
//   ПрименятьТолькоНапильник - Строка - Повторяемый параметр. Применять на желуде только напильник с указанным именем.
//   НеПрименятьНапильник - Строка - Повторяемый параметр. Не применять на желуде напильник с указанным именем.
//
&Аннотация("ОсобоеОбращение")
Процедура ПриСозданииОбъекта(
	ОтключитьВсеНапильники = Ложь,
	&Повторяемый ПрименятьТолькоНапильник = Неопределено,
	&Повторяемый НеПрименятьНапильник = Неопределено
)
	
	Для Каждого МестоПрименения Из ПрименятьТолькоНапильник Цикл
		Ожидаем.Что(МестоПрименения, "Ограничение ""только"" применения напильника должно быть задано строкой")
			.ИмеетТип("Строка");
	КонецЦикла;
	
	Для Каждого МестоПрименения Из НеПрименятьНапильник Цикл
		Ожидаем.Что(МестоПрименения, "Ограничение ""не"" применения напильника должно быть задано строкой")
			.ИмеетТип("Строка");
	КонецЦикла;
	
	Если ОтключитьВсеНапильники И ПрименятьТолькоНапильник.Количество() > 0 Тогда
		ВызватьИсключение "Нельзя одновременно отключать все напильники и задавать ограничения ""только""";
	КонецЕсли;

	Если ОтключитьВсеНапильники И НеПрименятьНапильник.Количество() > 0 Тогда
		ВызватьИсключение "Нельзя одновременно отключать все напильники и задавать ограничения ""не""";
	КонецЕсли;

	Если ПрименятьТолькоНапильник.Количество() > 0 И НеПрименятьНапильник.Количество() > 0 Тогда
		ВызватьИсключение "Нельзя одновременно задавать ограничения ""только"" и ""не""";
	КонецЕсли;
	
	_ОтключитьВсеНапильники = ОтключитьВсеНапильники;
	_ПрименятьТолькоНапильники = ПрименятьТолькоНапильник;
	_НеПрименятьНапильники = НеПрименятьНапильник;
	
КонецПроцедуры
