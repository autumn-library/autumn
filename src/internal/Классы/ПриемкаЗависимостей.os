#Использовать annotations
#Использовать asserts
#Использовать reflector

#Область ОписаниеПеременных

// ПрилепляторЧастиц - объект, который умеет прилеплять частицы к желудям.
Перем ПрилепляторЧастиц;

// РазворачивательАннотаций - разворачиватель аннотаций свойств и методов желудей.
Перем РазворачивательАннотаций;

// Поделка - Управляющий ioc-контейнер.
Перем Поделка;

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ПодготовитьПрилепляемыеЧастицы(ОпределениеЖелудя, ПереданныеПрилепляемыеЧастицы) Экспорт

	Если ПереданныеПрилепляемыеЧастицы = Неопределено Тогда
		ПереданныеПрилепляемыеЧастицы = Новый Массив;
	КонецЕсли;

	Если ПереданныеПрилепляемыеЧастицы.Количество() = ОпределениеЖелудя.ПрилепляемыеЧастицы().Количество() Тогда
		Возврат ПереданныеПрилепляемыеЧастицы;
	КонецЕсли;
	
	КоличествоБлестяшек = ПосчитатьКоличествоБлестяшек(ОпределениеЖелудя.ПрилепляемыеЧастицы());

	Если КоличествоБлестяшек <> ПереданныеПрилепляемыеЧастицы.Количество() Тогда
		ВызватьИсключение СтрШаблон(
			"При поиске желудя %1 количество переданных произвольных параметров отличается от количества параметров не-желудей/не-деталек.",
			ОпределениеЖелудя.Имя()
		);
	КонецЕсли;

	СчетчикИспользованияБлестяшек = 0;
	ПередаваемыеПрилепляемыеЧастицы = Новый Массив;
	Для Каждого ДанныеОПрилепляемойЧастице Из ОпределениеЖелудя.ПрилепляемыеЧастицы() Цикл
		
		Если ДанныеОПрилепляемойЧастице.ТипЧастицы() = ТипыПрилепляемыхЧастиц.Блестяшка() Тогда
			ПрилепляемаяЧастица = ПереданныеПрилепляемыеЧастицы[СчетчикИспользованияБлестяшек];
			СчетчикИспользованияБлестяшек = СчетчикИспользованияБлестяшек + 1;
		Иначе
			ПрилепляемаяЧастица = ПрилепляторЧастиц.НайтиПрилепляемуюЧастицу(ДанныеОПрилепляемойЧастице);
		КонецЕсли;

		ПередаваемыеПрилепляемыеЧастицы.Добавить(ПрилепляемаяЧастица);
	КонецЦикла;
	
	Возврат ПередаваемыеПрилепляемыеЧастицы;
	
КонецФункции

Функция ПрочитатьПрилепляемыеЧастицыВМетоде(Метод, ВладелецСвойств) Экспорт

	ПрилепляемыеЧастицы = Новый Массив;
	Для Каждого ПараметрМетода Из Метод.Параметры Цикл

		РазворачивательАннотаций.РазвернутьАннотацииСвойства(ПараметрМетода, ВладелецСвойств);

		ПрилепляемаяЧастица = ПрилепляторЧастиц.ДанныеОПрилепляемойЧастице(ПараметрМетода);
		ПрилепляемыеЧастицы.Добавить(ПрилепляемаяЧастица);

	КонецЦикла;

	Возврат ПрилепляемыеЧастицы;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПосчитатьКоличествоБлестяшек(ПрилепляемыеЧастицы)
	Количество = 0;
	Для Каждого Элемент Из ПрилепляемыеЧастицы Цикл
		Если Элемент.ТипЧастицы() = ТипыПрилепляемыхЧастиц.Блестяшка() Тогда
			Количество = Количество + 1;	
		КонецЕсли;
	КонецЦикла;
	Возврат Количество;
КонецФункции

#КонецОбласти

#Область Инициализация

Процедура ПриСозданииОбъекта(пПоделка, пРазворачивательАннотаций, пПрилепляторЧастиц)

	Поделка = пПоделка;
	РазворачивательАннотаций = пРазворачивательАннотаций;
	ПрилепляторЧастиц = пПрилепляторЧастиц;

КонецПроцедуры

#КонецОбласти