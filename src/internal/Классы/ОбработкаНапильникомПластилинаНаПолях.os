#Использовать reflector

Перем _Поделка;

Функция ОбработатьЖелудь(Желудь, ОпределениеЖелудя) Экспорт // BSLLS:UnusedParameters-off
	
	ОбработатьАннотацию("Пластилин", Желудь, ОпределениеЖелудя);
	ОбработатьАннотацию("Деталька", Желудь, ОпределениеЖелудя);
	
	Возврат Желудь;

КонецФункции

Процедура ОбработатьАннотацию(ИмяАннотации, Желудь, ОпределениеЖелудя)

	Рефлектор = Новый Рефлектор();
	
	Свойства = ОпределениеЖелудя.Свойства();
	Для Каждого Свойство Из Свойства Цикл

		Аннотация = РаботаСАннотациями.ПолучитьАннотацию(Свойство, ИмяАннотации); 
 
		Если Аннотация = Неопределено Тогда 
			Продолжить; 
		КонецЕсли; 

		ДанныеОПрилепляемойЧастице = ПрилеплениеЧастиц.ДанныеОПрилепляемойЧастице(Свойство);
		ПрилепляемаяЧастица = ПрилеплениеЧастиц.НайтиПрилепляемуюЧастицу(_Поделка, ДанныеОПрилепляемойЧастице);
		
		Рефлектор.УстановитьСвойство(Желудь, Свойство.Имя, ПрилепляемаяЧастица);

	КонецЦикла;

	Методы = ОпределениеЖелудя.НайтиМетодыСАннотациями(ИмяАннотации);
	Для Каждого Метод Из Методы Цикл

		Аннотация = РаботаСАннотациями.ПолучитьАннотацию(Метод, ИмяАннотации); 
 
		Если Аннотация = Неопределено Тогда 
			Продолжить; 
		КонецЕсли; 

		ПрототипИмениПрилепляемойЧастицы = СтрЗаменить(Метод.Имя, "Установить", "");

		ДанныеОПрилепляемойЧастице = ПрилеплениеЧастиц.ДанныеОПрилепляемойЧастице(Метод, ПрототипИмениПрилепляемойЧастицы);
		ПрилепляемаяЧастица = ПрилеплениеЧастиц.НайтиПрилепляемуюЧастицу(_Поделка, ДанныеОПрилепляемойЧастице);

		ПараметрыМетода = Новый Массив();
		ПараметрыМетода.Добавить(ПрилепляемаяЧастица);

		Рефлектор.ВызватьМетод(Желудь, Метод.Имя, ПараметрыМетода);
	КонецЦикла;

КонецПроцедуры

&Напильник(Порядок = 0)
Процедура ПриСозданииОбъекта(&Пластилин Поделка)
	_Поделка = Поделка;
КонецПроцедуры
