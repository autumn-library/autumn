#Использовать reflector

Перем _Поделка;
Перем _Рефлектор;

Функция ОбработатьЖелудь(Желудь, ОпределениеЖелудя) Экспорт // BSLLS:UnusedParameters-off
	
	ОбработатьАннотацию("Пластилин", Желудь, ОпределениеЖелудя);
	ОбработатьАннотацию("Деталька", Желудь, ОпределениеЖелудя);
	
	Возврат Желудь;

КонецФункции

Процедура ОбработатьАннотацию(ИмяАннотации, Желудь, ОпределениеЖелудя)
	
	Свойства = ОпределениеЖелудя.Свойства();
	Для Каждого Свойство Из Свойства Цикл

		Аннотация = РаботаСАннотациями.ПолучитьАннотацию(Свойство, ИмяАннотации); 
 
		Если Аннотация = Неопределено Тогда 
			Продолжить; 
		КонецЕсли; 

		ДанныеОПрилепляемойЧастице = ПрилеплениеЧастиц.ДанныеОПрилепляемойЧастице(Свойство, Неопределено);
		ПрилепляемаяЧастица = ПрилеплениеЧастиц.НайтиПрилепляемуюЧастицу(_Поделка, ДанныеОПрилепляемойЧастице, Неопределено);
		
		_Рефлектор.УстановитьСвойство(Желудь, Свойство.Имя, ПрилепляемаяЧастица);

	КонецЦикла;

	Методы = ОпределениеЖелудя.НайтиМетодыСАннотациями(ИмяАннотации);
	Для Каждого Метод Из Методы Цикл

		Аннотация = РаботаСАннотациями.ПолучитьАннотацию(Метод, ИмяАннотации); 
 
		Если Аннотация = Неопределено Тогда 
			Продолжить; 
		КонецЕсли; 

		ПрототипИмениПрилепляемойЧастицы = СтрЗаменить(Метод.Имя, "Установить", "");

		ДанныеОПрилепляемойЧастице = ПрилеплениеЧастиц.ДанныеОПрилепляемойЧастице(Метод, ПрототипИмениПрилепляемойЧастицы);
		ПрилепляемаяЧастица = ПрилеплениеЧастиц.НайтиПрилепляемуюЧастицу(_Поделка, ДанныеОПрилепляемойЧастице, Неопределено
		);

		ПараметрыМетода = Новый Массив();
		ПараметрыМетода.Добавить(ПрилепляемаяЧастица);

		_Рефлектор.ВызватьМетод(Желудь, Метод.Имя, ПараметрыМетода);
	КонецЦикла;

КонецПроцедуры

&Напильник
&Порядок(0)
Процедура ПриСозданииОбъекта(&Пластилин Поделка)
	_Поделка = Поделка;
	_Рефлектор = Новый Рефлектор();
КонецПроцедуры
