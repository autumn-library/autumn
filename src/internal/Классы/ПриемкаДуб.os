#Область ОписаниеПеременных

Перем Поделка;

#КонецОбласти

#Область ПрограммныйИнтерфейс

Процедура ПриДобавленииОпределенияЖелудя(Знач ОпределениеЖелудя) Экспорт

	ЭтоДуб = ОпределениеЖелудя.Прозвища().Найти("Дуб") <> Неопределено;
	Если НЕ ЭтоДуб Тогда
		Возврат;
	КонецЕсли;

	ИмяКорневойАннотации = "Завязь";
	МетодыЗавязи = ОпределениеЖелудя.НайтиМетодыСАннотациями(ИмяКорневойАннотации);

	Для Каждого МетодЗавязи Из МетодыЗавязи Цикл
		
		ИмяЖелудя = ПолучитьЧитательАннотаций().ПрочитатьИмяЖелудя(МетодЗавязи.Аннотации, ИмяКорневойАннотации, МетодЗавязи.Имя);
		ТипЖелудя = ПолучитьЧитательАннотаций().ПрочитатьТипЖелудя(МетодЗавязи, МетодЗавязи.Аннотации);

		ОпределениеЗавязи = СоздатьОпределениеЖелудя(
			ИмяЖелудя,
			ТипЖелудя,
			ОпределениеЖелудя.ТипЖелудя(), 
			МетодЗавязи,
			МетодЗавязи.Аннотации,
			ИмяКорневойАннотации
		);
		ПолучитьРепозиторийОпределенийЖелудей().СохранитьОпределениеЖелудя(ОпределениеЗавязи);

	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьЧитательАннотаций()
	Возврат Поделка.НайтиЖелудь("ЧитательАннотаций");
КонецФункции

Функция ПолучитьРепозиторийОпределенийЖелудей()
	Возврат Поделка.НайтиЖелудь("РепозиторийОпределенийЖелудей");
КонецФункции

Функция ПолучитьФабрикаЗавязей()
	Возврат Поделка.НайтиЖелудь("ФабрикаЗавязей");
КонецФункции

Функция ПолучитьРазворачивательАннотаций()
	Возврат Поделка.НайтиЖелудь("РазворачивательАннотаций");
КонецФункции

Функция ПолучитьПрилепляторЧастиц()
	Возврат Поделка.НайтиЖелудь("ПрилепляторЧастиц");
КонецФункции

Функция СоздатьОпределениеЖелудя(ИмяЖелудя, ТипЖелудя, ВладелецСвойств, Конструктор, Аннотации, ИмяКорневойАннотации)
	
	Завязь = ПолучитьФабрикаЗавязей().СоздатьЗавязь(ВладелецСвойств, Конструктор);
	
	ПрилепляемыеЧастицы = ПрочитатьПрилепляемыеЧастицыВМетоде(Конструктор, ВладелецСвойств);
	Характер = ПолучитьЧитательАннотаций().ПрочитатьХарактерЖелудя(Аннотации);
	Прозвища = ПолучитьЧитательАннотаций().ПрочитатьПрозвища(Аннотации, ИмяЖелудя);
	Порядок = ПолучитьЧитательАннотаций().ПрочитатьПорядок(Аннотации);
	Верховный = ПолучитьЧитательАннотаций().ПрочитатьПризнакВерховногоЖелудя(Аннотации);
	Спецификация = ПолучитьЧитательАннотаций().ПрочитатьСпецификацию(Аннотации);
	КорневаяАннотация = ПолучитьЧитательАннотаций().ПрочитатьКорневуюАннотацию(Аннотации, ИмяКорневойАннотации);

	ОпределениеЖелудя = Новый ОпределениеЖелудя(
		ПолучитьРазворачивательАннотаций(),
		ТипЖелудя,
		ИмяЖелудя,
		Характер,
		ПрилепляемыеЧастицы,
		Завязь,
		Прозвища,
		Порядок,
		Верховный,
		Спецификация,
		КорневаяАннотация
	);
	
	Возврат ОпределениеЖелудя;
	
КонецФункции

Функция ПрочитатьПрилепляемыеЧастицыВМетоде(Метод, ВладелецСвойств)

	ПрилепляемыеЧастицы = Новый Массив;
	Для Каждого ПараметрМетода Из Метод.Параметры Цикл

		ПолучитьРазворачивательАннотаций().РазвернутьАннотацииСвойства(ПараметрМетода, ВладелецСвойств);

		ПрилепляемаяЧастица = ПолучитьПрилепляторЧастиц().ДанныеОПрилепляемойЧастице(ПараметрМетода);
		ПрилепляемыеЧастицы.Добавить(ПрилепляемаяЧастица);

	КонецЦикла;

	Возврат ПрилепляемыеЧастицы;

КонецФункции

#КонецОбласти

#Область Инициализация

&Приемка
Процедура ПриСозданииОбъекта(&Пластилин("Поделка") пПоделка)
	
	Поделка = пПоделка;
	
КонецПроцедуры

#КонецОбласти