#native

Функция ДанныеОПрилепляемойЧастице(Свойство, Знач ИмяСвойства = Неопределено) Экспорт

	Если ИмяСвойства = Неопределено Тогда
		ИмяСвойства = Свойство.Имя;
	КонецЕсли;

	АннотацияПластилин = РаботаСАннотациями.ПолучитьАннотацию(Свойство, "Пластилин");
	АннотацияДеталька = РаботаСАннотациями.ПолучитьАннотацию(Свойство, "Деталька");
	АннотацияБлестяшка = РаботаСАннотациями.ПолучитьАннотацию(Свойство, "Блестяшка");

	Если АннотацияПластилин <> Неопределено Тогда
		ТипЧастицы = ТипыПрилепляемыхЧастиц.Желудь();
		ИмяЧастицы = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(
			АннотацияПластилин,
			"Значение",
			ИмяСвойства
		);
		
		Аннотация = АннотацияПластилин;

	ИначеЕсли АннотацияДеталька <> Неопределено Тогда
		ТипЧастицы = ТипыПрилепляемыхЧастиц.Деталька();
		ИмяЧастицы = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(
			АннотацияДеталька,
			"Значение",
			ИмяСвойства
		);
		Аннотация = АннотацияДеталька;
	ИначеЕсли АннотацияБлестяшка <> Неопределено Тогда
		ТипЧастицы = ТипыПрилепляемыхЧастиц.Блестяшка();
		ИмяЧастицы = ИмяСвойства;
		Аннотация = АннотацияБлестяшка;
	Иначе
		ВызватьИсключение СтрШаблон(
			"Неизвестный тип прилепляемой частицы в свойстве/методе %1",
			ИмяСвойства
		);
	КонецЕсли;
	
	ПрилепляемаяЧастица = Новый ПрилепляемаяЧастица(ТипЧастицы, ИмяЧастицы, Аннотация);

	АннотацияТабакерка = РаботаСАннотациями.ПолучитьАннотацию(Свойство, "Табакерка");
	Если АннотацияТабакерка <> Неопределено Тогда
		ТипЧастицы = ТипыПрилепляемыхЧастиц.Табакерка();
		Аннотация = АннотацияТабакерка;
		БазоваяПрилепляемаяЧастица = ПрилепляемаяЧастица;

		ПрилепляемаяЧастица = Новый ПрилепляемаяЧастица(ТипЧастицы, ИмяЧастицы, Аннотация, БазоваяПрилепляемаяЧастица);
	КонецЕсли;

	Возврат ПрилепляемаяЧастица;

КонецФункции

Функция НайтиПрилепляемуюЧастицу(Поделка, ДанныеОПрилепляемойЧастице, Знач ПрилепляемыеЧастицы = Неопределено) Экспорт

	ТипЧастицы = ДанныеОПрилепляемойЧастице.ТипЧастицы();
	ИмяЧастицы = ДанныеОПрилепляемойЧастице.ИмяЧастицы();
	Аннотация = ДанныеОПрилепляемойЧастице.Аннотация();
	БазоваяПрилепляемаяЧастица = ДанныеОПрилепляемойЧастице.БазоваяПрилепляемаяЧастица();

	Если ТипЧастицы = ТипыПрилепляемыхЧастиц.Табакерка() Тогда
		ПрилепляемаяЧастица = Новый Табакерка(Поделка, БазоваяПрилепляемаяЧастица);
	ИначеЕсли ТипЧастицы = ТипыПрилепляемыхЧастиц.Желудь() Тогда

		ТипЖелудя = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(
			Аннотация,
			"Тип",
			ТипыПрилепляемыхЖелудей.Желудь()
		);

		Если ПрилепляемыеЧастицы = Неопределено Тогда
			ПараметрыАннотации = РаботаСАннотациями.ПолучитьПараметрыАннотации(Аннотация, "Блестяшка");
			ПрилепляемыеЧастицы = Новый Массив;
			Для Каждого ПараметрБлестяшка Из ПараметрыАннотации Цикл
				ПрилепляемыеЧастицы.Добавить(ПараметрБлестяшка.Значение);
			КонецЦикла;
		КонецЕсли;

		ПоддерживаемыеТипыПрилепляемыхЖелудей = Новый Массив;
		ПоддерживаемыеТипыПрилепляемыхЖелудей.Добавить(ТипыПрилепляемыхЖелудей.Массив());
		ПоддерживаемыеТипыПрилепляемыхЖелудей.Добавить(ТипыПрилепляемыхЖелудей.Соответствие());
		ПоддерживаемыеТипыПрилепляемыхЖелудей.Добавить(ТипыПрилепляемыхЖелудей.ТаблицаЗначений());
		
		Если ТипЖелудя = ТипыПрилепляемыхЖелудей.Желудь() Тогда
			ПрилепляемаяЧастица = Поделка.НайтиЖелудь(ИмяЧастицы, ПрилепляемыеЧастицы);
		ИначеЕсли ЭлементЕстьВМассиве(ТипЖелудя, ПоддерживаемыеТипыПрилепляемыхЖелудей) Тогда
			ПрилепляемаяЧастица = Поделка.НайтиЖелуди(ИмяЧастицы, ПрилепляемыеЧастицы, ТипЖелудя);
		Иначе
			ВызватьИсключение "Неизвестный тип внедряемого через &Пластилин значения";
		КонецЕсли;

	ИначеЕсли ТипЧастицы = ТипыПрилепляемыхЧастиц.Деталька() Тогда

		ЗначениеПоУмолчанию = РаботаСАннотациями.ПолучитьЗначениеПараметраАннотации(
			Аннотация, 
			"ЗначениеПоУмолчанию", 
			Неопределено,
			Истина
		);

		ПрилепляемаяЧастица = Поделка.НайтиДетальку(ИмяЧастицы, ЗначениеПоУмолчанию);
	Иначе
		ВызватьИсключение "Неизвестная аннотация " + Аннотация.Имя();
	КонецЕсли;

	Возврат ПрилепляемаяЧастица;

КонецФункции

Функция ЭлементЕстьВМассиве(Элемен, Массив)

	Для Каждого ТекущийЭлемент из Массив Цикл
		Если ТекущийЭлемент = Элемен Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;

	Возврат Ложь;
	
КонецФункции