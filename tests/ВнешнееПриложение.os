#Использовать ".."
#Использовать asserts
#Использовать "./ТестКлассы"
#Использовать "./Заготовки"

Процедура ПослеЗапускаТеста() Экспорт
	Осень.ОчиститьЗаготовкиДляАвтоИнициализации();
КонецПроцедуры

&Тест
Процедура ПриЗапускеПриложенияОтрабатываетРогатка() Экспорт

	// Дано
	Поделка = Новый Поделка;

	// Когда
	Поделка.ЗапуститьПриложение();

	// Тогда
	Массив = Поделка.НайтиЖелудь("Массив");
	Ожидаем.Что(Массив).ИмеетДлину(5);

КонецПроцедуры

&Тест
Процедура ИнициализацияПоделкиСоСтороныПриложенияЧерезСтатическийИнициализатор() Экспорт

	// Когда
	Осень.ДобавитьЗаготовкуДляАвтоИнициализации(Тип("ТестовыйИнициализатор"));

	// Дано
	Поделка = Новый Поделка;

	// Когда
	Поделка.ЗапуститьПриложение();

	// Тогда
	Желудь = Поделка.НайтиЖелудь("ЖелудьНижнегоУровня");
	Ожидаем.Что(Желудь).Не_().Равно(Неопределено);

КонецПроцедуры

&Тест
Процедура ЗаготовкиВСтатическийИнициализаторДобавляютсяТолькоОдинРаз() Экспорт

	// Дано
	Осень.ДобавитьЗаготовкуДляАвтоИнициализации("ТестовыйИнициализатор");
	Осень.ДобавитьЗаготовкуДляАвтоИнициализации("ТестовыйИнициализатор");

	// Когда
	Заготовки = Осень.ПолучитьЗаготовкиДляАвтоИнициализации();
	
	// Тогда
	Ожидаем.Что(Заготовки, "Заготовки задублированы").ИмеетДлину(0); // Не используется

КонецПроцедуры

&Тест
Процедура ЗаготовкаРегистрируетПодчиненныеЖелуди() Экспорт

	// Дано
	Поделка = Новый Поделка;

	// Когда
	Поделка.ЗапуститьПриложение();

	// Тогда
	Желудь = Поделка.НайтиЖелудь("ЖелудьНижнегоУровня");
	Ожидаем.Что(Желудь).Не_().Равно(Неопределено);

КонецПроцедуры

&Тест
Процедура ВЗаготовкуМожноПрилепитьДетальку() Экспорт

	// Дано
	Поделка = Новый Поделка;

	// Когда
	Поделка.ЗапуститьПриложение();

	// Тогда
	Желудь = Поделка.НайтиЖелудь("ЗаготовкаСДеталькой");
	Ожидаем.Что(Желудь.ПростаяНастройка()).Равно("Значение простой настройки");

КонецПроцедуры

&Тест
Процедура ВсеРогаткиЗапускаютсяКорректно() Экспорт

	// Дано
	Поделка = Новый Поделка;

	// Когда
	Поделка.ЗапуститьПриложение();

	// Тогда
	ДолгаяРогатка = Поделка.НайтиЖелудь("ДолгаяРогатка");
	Ожидаем.Что(ДолгаяРогатка.Значение).Равно(0);

	// Когда
	Приостановить(1500);

	// Тогда
	Ожидаем.Что(ДолгаяРогатка.Значение).Равно(1);

КонецПроцедуры
