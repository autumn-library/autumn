#Использовать ".."
#Использовать asserts
#Использовать "."

&Тест
Процедура ИнициализацияКонтекстаСоСтороныПриложения() Экспорт

	// Дано
	КонтекстПриложения = Новый КонтекстПриложения;

	// Когда
	КонтекстПриложения
		.ЗарегистрироватьЗаготовку(Тип("ТестовыйИнициализатор"))
		.ЗапуститьПриложение();

	// Тогда
	Желудь = КонтекстПриложения.ПолучитьЖелудь("ЖелудьНижнегоУровня");
	Ожидаем.Что(Желудь).Не_().Равно(Неопределено);

КонецПроцедуры

&Тест
Процедура ИнициализацияКонтекстаСоСтороныПриложенияЧерезСтатическийИнициализатор() Экспорт

	// Когда
	Осень.ЗарегистрироватьЗаготовкуДляАвтоИнициализации(Тип("ТестовыйИнициализатор"));

	// Дано
	КонтекстПриложения = Новый КонтекстПриложения;

	// Когда
	КонтекстПриложения.ЗапуститьПриложение();

	// Тогда
	Желудь = КонтекстПриложения.ПолучитьЖелудь("ЖелудьНижнегоУровня");
	Ожидаем.Что(Желудь).Не_().Равно(Неопределено);

КонецПроцедуры

&Тест
Процедура ПриЗапускеПриложенияОтрабатываетЗаготовка() Экспорт

	// Дано
	КонтекстПриложения = Новый КонтекстПриложения;
	КонтекстПриложения.ЗарегистрироватьДуб(Тип("ГлавныйДуб"));

	// Когда
	КонтекстПриложения
		.ЗарегистрироватьРогатку(Тип("РогаткаМеняющаяМассив"))
		.ЗапуститьПриложение();

	// Тогда
	Массив = КонтекстПриложения.ПолучитьЖелудь("Массив");
	Ожидаем.Что(Массив).ИмеетДлину(2);
	
КонецПроцедуры

&Тест
Процедура ПриЗапускеПриложенияОтрабатываетЗаготовкаСПараметрамиЗапуска() Экспорт

	// Дано
	КонтекстПриложения = Новый КонтекстПриложения;
	КонтекстПриложения.ЗарегистрироватьДуб(Тип("ГлавныйДуб"));
	ПараметрЗапуска = 1;

	// Когда
	КонтекстПриложения
		.ЗарегистрироватьРогатку(Тип("РогаткаСПараметрамиЗапуска"))
		.ЗапуститьПриложение(ПараметрЗапуска);

	// Тогда
	Массив = КонтекстПриложения.ПолучитьЖелудь("Массив");
	Ожидаем.Что(Массив[1]).Равно(ПараметрЗапуска);
	
КонецПроцедуры
